buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
}

plugins {
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'base'
	id 'idea'
	id 'com.diffplug.gradle.spotless' version '3.27.0'

}

allprojects {
	group = 'com.zup'
	version = '1.0.0'

	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.diffplug.gradle.spotless'

	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}

	spotless {
		java {
			googleJavaFormat()
		}
		format 'misc', {
			target '**/*.gradle', '**/*.md', '**/.gitignore'

			trimTrailingWhitespace()
			indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
			endWithNewline()
		}
	}
}


subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11


	configurations {
		developmentOnly
		runtimeClasspath {
			extendsFrom developmentOnly
		}
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencyManagement {
		imports {
			mavenBom 'de.codecentric:spring-boot-admin-dependencies:2.2.3'
			mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.RELEASE'
			mavenBom 'org.springframework.cloud:spring-cloud-contract-dependencies:2.2.3.RELEASE'
		}
	}

	configurations {
		testAgent {
			transitive = false
		}
	}

	test {
		useJUnitPlatform()
	}

}
